{ lib, config, ... }:
let
  inherit (config.unflake) pkgs;
  inherit (import ./../dev/modules/_lib lib) inputsExpr nixCode;

  inputsString = ''
    # DO-NOT-EDIT: Generated by github:vic/flake-file for unflake.
    # To re-generate use:   nix-shell . -A unflake.env --run write-inputs
  ''
  + (nixCode (inputsExpr config.flake-file.inputs));

  inputsFile = pkgs.stdenvNoCC.mkDerivation {
    name = "inputs";
    passAsFile = [ "inputsString" ];
    inherit inputsString;
    phases = [ "copy" ];
    copy = ''
      cp $inputsStringPath $out
    '';
  };

  write-inputs = pkgs.writeShellApplication {
    name = "write-inputs";
    text = ''
      cp ${inputsFile} "''${1:-inputs.nix}"
      ${lib.getExe pkgs.nixfmt} "''${1:-inputs.nix}"
    '';
  };

  write-unflake = pkgs.writeShellApplication {
    name = "write-unflake";
    text = ''
      nix-shell "${config.unflake.url}" -A unflake-shell --run "unflake -i ${inputsFile} $*"
    '';
  };

  env = pkgs.mkShell {
    buildInputs = [
      write-inputs
      write-unflake
    ];
  };
in
{
  options.unflake = {
    url = lib.mkOption {
      type = lib.types.str;
      description = "unflake archive url";
      default = "https://codeberg.org/goldstein/unflake/archive/main.tar.gz";
    };

    pkgs = lib.mkOption {
      type = lib.types.raw;
      description = "nixpkgs instance for unflake generator";
      default = import <nixpkgs> { };
    };

    env = lib.mkOption {
      type = lib.types.raw;
      readOnly = true;
      internal = true;
      default = env;
    };
  };
}
